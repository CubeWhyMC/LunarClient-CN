plugins {
    id 'java'
    id 'idea'
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.8.21'
    id 'com.github.weave-mc.weave-gradle' version 'bcf6ab0279'
    id 'java-library'
    id("org.jetbrains.kotlin.plugin.serialization") version "1.9.0-Beta"
}

group = 'org.cubewhy.lunarcn'
version = 'next-gen build 3'

minecraft.version("1.8.9") // Minecraft Version

configurations {
    include
    includeApi
    api.extendsFrom(includeApi)
    implementation.extendsFrom(include)
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = "https://nexus.velocitypowered.com/repository/maven-public/" }
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://jitpack.io/" }
}

dependencies {
    include fileTree(dir: "libs", include: ["*.jar"])

    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    includeApi 'org.ow2.asm:asm:9.4'
    includeApi 'org.ow2.asm:asm-util:9.4'
    includeApi 'org.ow2.asm:asm-tree:9.4'

    compileOnly 'org.spongepowered:mixin:0.8.5'

    include 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0'
    include 'com.google.code.gson:gson:2.10.1'
    include "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    include 'io.earcam.wrapped:com.sun.tools.attach:1.8.0_jdk8u172-b11'
//    include 'com.github.cubewhy:LauncherLib:master-SNAPSHOT' // the Lunar LauncherLib

    compileOnly 'org.apache.commons:commons-lang3:3.12.0'
    compileOnly group: 'org.lwjgl.lwjgl', name: 'lwjgl', version: '2.9.4-nightly-20150209'
}

shadowJar {
    // fatJar
    archiveClassifier.set("fatjar")
    configurations = [project.configurations.include, project.configurations.includeApi]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    mainClassName = "org.cubewhy.lunarcn.Main"

    exclude "LICENSE.txt"

    exclude "**/module-info.class"
}

application {
    // run task
    executableDir = projectDir.getPath()
    mainClass = 'org.cubewhy.lunarcn.Main'
}

jar {
    manifest {
        attributes(
                "Main-Class": "org.cubewhy.lunarcn.Main", // 主类
                "Premain-Class": "org.cubewhy.lunarcn.loader.bootstrap.AgentKt", // ModLoader,
                "Agent-Class": "org.cubewhy.lunarcn.loader.bootstrap.AgentKt",
                "MixinConfigs": "mixins.lunarcn.json",
                "ModSide": "CLIENT",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder": "0",
        )
    }
}

kotlin {
    jvmToolchain(8)
    explicitApiWarning()
}